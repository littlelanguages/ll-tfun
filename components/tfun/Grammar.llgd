uses "./Scanner.llld";

Program
    : Element {";" Element}
    ;

Element
    : Expression
    | DataDeclaration
    | ImportStatement
    ;

Expression
    : Relational {Relational}
    ;

Relational
    : Additive ["==" Additive]
    ;

Additive
    : Multiplicative {AdditiveOps Multiplicative}
    ;

AdditiveOps
    : "+"
    | "-"
    ;

Multiplicative
    : Projection {MultiplicativeOps Projection}
    ;

MultiplicativeOps
    : "*"
    | "/"
    ;

Projection
    : Factor {"." LowerIdentifier}
    ;

Factor
    : "(" [Expression {"," Expression}] ")"
    | LiteralInt
    | LiteralString
    | "True"
    | "False"
    | "\" Parameter {Parameter} [":" Type] "=" Expression
    | "let" ["rec"] ValueDeclaration {"and" ValueDeclaration} ["in" Expression]
    | "if" "(" Expression ")" Expression "else" Expression
    | UpperIdentifier ["." Identifier]
    | LowerIdentifier
    | "match" Expression "with" ["|"] Case {"|" Case}
    | "{" [LowerIdentifier ":" Expression {"," LowerIdentifier ":" Expression} ["|" Expression ]] "}"
    | "builtin" LiteralString
    ;

Identifier
    : LowerIdentifier
    | UpperIdentifier
    ;

ValueDeclaration
    : LowerIdentifier["*"] {Parameter} [":" Type] "=" Expression
    ;

Parameter
    : LowerIdentifier
    | "(" LowerIdentifier ":" Type ")"
    ;

Case
    : Pattern "->" Expression
    ;

Pattern
    : "(" [Pattern {"," Pattern}] ")"
    | LiteralInt
    | LiteralString
    | "True"
    | "False"
    | LowerIdentifier
    | UpperIdentifier ["." UpperIdentifier] {Pattern}
    | "{" [LowerIdentifier [":" Pattern] {"," LowerIdentifier [":" Pattern]} ["|" Pattern ]] "}"
    ;

DataDeclaration
    : "data" TypeDeclaration {"and" TypeDeclaration}
    ;

TypeDeclaration
    : UpperIdentifier["*" | "-"] {LowerIdentifier} "=" ConstructorDeclaration {"|" ConstructorDeclaration}
    ;

ConstructorDeclaration
    : UpperIdentifier {Type}
    ;

Type
    : ADTType {"->" ADTType}
    ;

ADTType
    : UpperIdentifier ["." UpperIdentifier] {Type}
    | TermType
    ;

TermType
    : LowerIdentifier
    | "(" [Type {"*" Type}] ")"
    | "{" [LowerIdentifier ":" Type {"," LowerIdentifier ":" Type} ["|" Type]] "}"
    ;

ImportStatement
    : "import" ImportItems "from" LiteralString
    ;

ImportItems
    : "*" ["as" UpperIdentifier]
    | ImportItem {"," ImportItem}
    ;

ImportItem
    : UpperIdentifier ["*" | "-"]
    | LowerIdentifier ["as" LowerIdentifier] ["*"]
    ;
