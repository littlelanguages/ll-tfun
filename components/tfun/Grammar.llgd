uses "./Scanner.llld";

Program
    : Element {";" Element}
    ;

Element
    : Expression
    | DataDeclaration
    | TypeAliasDeclarations
    | ImportStatement
    ;

Expression
    : BooleanOr {"|>" BooleanOr}
    ;

BooleanOr
    : BooleanAnd {"||" BooleanAnd}
    ;

BooleanAnd
    : Equality {"&&" Equality}
    ;

Equality
    : AppendCons [EqualityOps AppendCons]
    ;

EqualityOps
    : "=="
    | "/="
    | "<"
    | "<="
    | ">"
    | ">="
    ;

AppendCons
    : Additive {AppendConsOps Additive}
    ;

AppendConsOps
    : "::"
    | "++"
    ;

Additive
    : Multiplicative {AdditiveOps Multiplicative}
    ;

AdditiveOps
    : "+"
    | "-"
    ;

Multiplicative
    : Apply {MultiplicativeOps Apply}
    ;

MultiplicativeOps
    : "*"
    | "/"
    ;

Apply
    : Typing {Typing}
    ;

Typing
    : Projection [":" TypeType]
    ;

Projection
    : Factor {"." LowerIdentifier}
    ;

Factor
    : "(" [Expression {"," Expression}] ")"
    | LiteralInt
    | LiteralString
    | LiteralChar
    | "True"
    | "False"
    | "\" Parameter {Parameter} [":" TypeType] "=" Expression
    | "let" ["rec"] ValueDeclaration {"and" ValueDeclaration} ["in" Expression]
    | "if" "(" Expression ")" Expression "else" Expression
    | UpperIdentifier ["." Identifier]
    | LowerIdentifier
    | "match" Expression "with" ["|"] Case {"|" Case}
    | "[" [Expression {"," Expression}] "]"
    | "{" [LowerIdentifier ":" Expression {"," LowerIdentifier ":" Expression} ["|" Expression ]] "}"
    | "builtin" LiteralString
    ;

Identifier
    : LowerIdentifier
    | UpperIdentifier
    ;

ValueDeclaration
    : LowerIdentifier["*"] {Parameter} [":" TypeType] "=" Expression
    ;

Parameter
    : LowerIdentifier
    | "(" LowerIdentifier ":" TypeType ")"
    ;

Case
    : Pattern "->" Expression
    ;

Pattern
    : PatternTerm {"::" PatternTerm}
    ;

PatternTerm
    : "(" [Pattern {"," Pattern}] ")"
    | LiteralInt
    | LiteralString
    | LiteralChar
    | "True"
    | "False"
    | LowerIdentifier
    | UpperIdentifier ["." UpperIdentifier] {Pattern}
    | "[" [Pattern {"," Pattern}] "]"
    | "{" [LowerIdentifier [":" Pattern] {"," LowerIdentifier [":" Pattern]} ["|" Pattern ]] "}"
    ;

DataDeclaration
    : "data" TypeDeclaration {"and" TypeDeclaration}
    ;

TypeDeclaration
    : UpperIdentifier["*" | "-"] {LowerIdentifier} "=" ConstructorDeclaration {"|" ConstructorDeclaration}
    ;

ConstructorDeclaration
    : UpperIdentifier {TypeType}
    ;

TypeType
    : ADTType {"->" ADTType}
    ;

ADTType
    : UpperIdentifier ["." UpperIdentifier] {TermType | UpperIdentifier ["." UpperIdentifier]}
    | TermType
    ;

TermType
    : LowerIdentifier
    | "(" [TypeType {"*" TypeType}] ")"
    | "{" [LowerIdentifier ":" TypeType {"," LowerIdentifier ":" TypeType} ["|" TypeType]] "}"
    ;

TypeAliasDeclarations
    : "type" TypeAliasDeclaration
    ;

TypeAliasDeclaration
    : UpperIdentifier["*" | "-"] {LowerIdentifier} "=" TypeType
    ;

ImportStatement
    : "import" ImportItems "from" LiteralString
    ;

ImportItems
    : "*" ["as" UpperIdentifier]
    | ImportItem {"," ImportItem}
    ;

ImportItem
    : UpperIdentifier ["*" | "-"]
    | LowerIdentifier ["as" LowerIdentifier] ["*"]
    ;
