import * as Char from "../../../Data/Char.tfun" ;
import * as Dict from "../../../Data/Dict.tfun" ;
import * as Int from "../../../Data/Integer.tfun" ;
import * as List from "../../../Data/List.tfun" ;
import * as Maybe from "../../../Data/Maybe.tfun" ;
import * as String from "../../../Data/String.tfun" ;
import * from "../../../Data/Tuple.tfun" ;
import * as Console from "../../../System/IO/Console.tfun" ;
import Location*, combine*, toString as locationToString* from "./Location.tfun" ;
import Token*, TokenType*, toString* from "./Token.tfun" ;

type Scanner* = {
  input: String,
  inputLength: Int,
  offset: Int,
  column: Int,
  line: Int,
  token: Token
};

let keywords: Dict.Dict String TokenType = 
  Dict.fromList 
    [ ("as", As)
    , ("False", TFalse)
    , ("True", TTrue)
    ] ;

let atEnd* (scanner: Scanner): Bool =
  scanner.offset >= scanner.inputLength ;

let currentCharacter (scanner: Scanner): Char =
  if (atEnd scanner)
    '\0'
  else
    String.atElse '\0' scanner.offset scanner.input ;

let skipCharacter (scanner: Scanner): Scanner =
  if (atEnd scanner)
    scanner
  else if (currentCharacter scanner == '\n')
    { offset: scanner.offset + 1, column: 1, line: scanner.line + 1 | scanner }
  else
    { offset: scanner.offset + 1, column: scanner.column + 1 | scanner } ;

let nextCharacter (scanner: Scanner): (Scanner * Char) =
  if (atEnd scanner)
    (scanner, '\0')
  else
    ( skipCharacter scanner
    , currentCharacter scanner
    ) ;

let locationAt (scanner: Scanner): Location =
  Point { line: scanner.line, column: scanner.column, offset: scanner.offset } ;
 
let skipWhile (predicate: Char -> Bool) (scanner: Scanner) (previous: Scanner): (Scanner * Location) =
  let rec loop (scanner: Scanner) (previous: Scanner): (Scanner * Location) =
    if (predicate (currentCharacter scanner))
      loop (skipCharacter scanner) scanner
    else
      (scanner, locationAt previous)
  in
  loop scanner previous;

let isWhitespace* (c: Char): Bool =
  let cv = Char.toInt c
  in cv /= 0 && cv <= 32 ;

let assignToken (typ: TokenType) (location: Location) (lexeme: String) (scanner: Scanner): Scanner =
  let token = { typ: typ, location: location, lexeme: lexeme }
  in
    { token: token | scanner } ;

let skipWhitespace* scanner =
  skipWhile isWhitespace scanner scanner |> fst ;

let matchOnPredicate (scanner: Scanner) (predicate: Char -> Bool) (constructor: String -> (Location -> a)): (Scanner * a) =
  let startLocation = locationAt scanner
  and skipWhileResult = skipCharacter scanner |> skipWhile predicate scanner
  and scannerp = skipWhileResult |> fst
  and endLocation = skipWhileResult |> snd
  in
    ( scannerp
    , constructor (String.slice scanner.offset scannerp.offset scanner.input) (combine startLocation endLocation)
    ) ;

let matchCharacter (char: Char) (scanner: Scanner): Maybe Scanner =
  let c = currentCharacter scanner
  in
    if (c == char)
      skipCharacter scanner |> Just
    else
      Nothing ;

let matchLiteralChar (scanner: Scanner): Scanner =
  let matchClosingQuote (scannerp: Scanner): Scanner =
    match matchCharacter '\'' scannerp with
    | Nothing -> assignToken ERROR (locationAt scanner) "Unterminated char literal" scannerp
    | Just scannerp2 -> assignToken LiteralChar (combine (locationAt scanner) (locationAt scannerp)) (String.slice scanner.offset scannerp2.offset scanner.input) scannerp2
  in
    match nextCharacter (skipCharacter scanner) with
    | (scannerp2, '\0') -> assignToken ERROR (locationAt scanner) "Unterminated char literal" scannerp2
    | (scannerp2, '\\') ->
      let scannerp3 = skipCharacter scannerp2
      in
        ( match currentCharacter scannerp2 with
          | 'n' -> matchClosingQuote scannerp3
          | '\\' -> matchClosingQuote scannerp3
          | '\'' -> matchClosingQuote scannerp3
          | '0' -> matchClosingQuote scannerp3
          | _ -> assignToken ERROR (locationAt scanner) "Invalid literal char escape sequence" scannerp3
        )
    | (scannerp2, c) ->
      matchClosingQuote scannerp2 ;

let rec literalStringLoop (scanner: Scanner): Maybe Scanner =
  if (atEnd scanner)
    Nothing
  else
    let c = currentCharacter scanner
    and scannerp = skipCharacter scanner
    in
      if (c == '"')
        scanner |> Just
      else if (c == '\\')
        let scannerp2 = skipCharacter scannerp
        in
          match currentCharacter scannerp with
          | '"' -> literalStringLoop scannerp2
          | 'n' -> literalStringLoop scannerp2
          | '\\' -> literalStringLoop scannerp2
          | _ -> Nothing
      else
        literalStringLoop scannerp;

let matchLiteralString scanner =
  let startLocation = locationAt scanner
  and scannerp = skipCharacter scanner
  in
    match literalStringLoop scannerp with
    | Nothing -> assignToken ERROR startLocation "Illegal string literal" scannerp
    | Just scannerEnd -> 
        let scannerp2 = skipCharacter scannerEnd
        in
          assignToken LiteralString (combine (locationAt scanner) (locationAt scannerEnd)) (String.slice scanner.offset scannerp2.offset scanner.input) scannerp2 ;

let matchIdentifier (identifierConstructor: TokenType) (scanner: Scanner): Scanner =
  let matched = matchOnPredicate scanner Char.isAlpha (\t l = { text: t, location: l })
  and scannerp = matched |> fst
  and identifier = matched |> snd
  in
    match Dict.get identifier.text keywords with
    | Just constructor -> assignToken constructor identifier.location identifier.text scannerp
    | Nothing -> assignToken identifierConstructor identifier.location identifier.text scannerp ;

let matchLiteralInt (scanner: Scanner): Scanner =
  let matched = matchOnPredicate scanner Char.isDigit (\t l = { text: t, location: l })
  and scannerp = matched |> fst
  and literal = matched |> snd
  in
    assignToken LiteralInt literal.location literal.text scannerp ;

let next* (scanner: Scanner): Scanner =
  let scannerp = skipWhitespace scanner
  in
    if (atEnd scannerp)
      assignToken EOS (locationAt scannerp) "" scanner
    else
      let c = currentCharacter scannerp
      in
        if (Char.isUpper c)
          matchIdentifier UpperIdentifier scannerp
        else if (Char.isLower c)
          matchIdentifier LowerIdentifier scannerp
        else if (Char.isDigit c)
          matchLiteralInt scannerp
        else if (c == '"')
          matchLiteralString scannerp
        else if (c == '\'')
          matchLiteralChar scannerp
        else
          match c with
          | ',' -> assignToken Comma (locationAt scannerp) "," (skipCharacter scannerp)
          | _ -> assignToken ERROR (locationAt scannerp) (Char.toString c) (skipCharacter scannerp) ;

let fromString* (input: String): Scanner =
  { input: input
  , inputLength: String.length input
  , offset: 0
  , column: 1
  , line: 1
  , token: { typ: EOS, location: Point { line: 1, column: 1, offset: 0 }, lexeme: "" }
  } |> next
